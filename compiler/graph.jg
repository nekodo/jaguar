import * from '//compiler/prelude.jg'

dfs g visited v = let
  visit r e = case (contains e r || contains e visited) of
    True -> r
    False -> concat (dfs g (push v (concat r visited)) e) r
  in case (lookup v g) of
    Just es -> enqueue v $ foldr visit [] es
    Nothing -> []

fullDfs g = let
  visit result v _ = case (contains v result) of
    True -> result
    False -> concat (dfs g result v) result
  in foldTrie visit [] g

scc g = let
  invertedG = let
    invertEdge v ig e = case (lookup e ig) of
      Just vs -> insert e (push v vs) ig
      Nothing -> insert e [v] ig
    invert ig v es = let
      ig2 = case (lookup v ig) of
        Just _ -> ig
        Nothing -> insert v [] ig
      in foldl (invertEdge v) ig2 es
    in foldTrie invert Empty g
  assembleCc (ig, ccs) v = case (exists (contains v) ccs) of
      True -> (ig, ccs)
      False -> let
        cc = dfs ig [] v
        ig2 = foldl (\g v -> remove v g) ig cc
        in (ig2, push cc ccs)
  firstDfs = fullDfs g
  ccs = snd (foldl assembleCc (invertedG, []) firstDfs)
  in ccs

sccSorted g = let
  //_ = debug ('sccSorted for size = ' ++ intToString (size g))
  ccs = scc g
  topSort ccs = let
    g2g = let
      f r (i, cc) = foldl (\r c -> insert c i r) r cc
      in foldl f Empty (zipWithIndex ccs)
    addGraph r v es = let
      rv = fromJust $ lookup v g2g
      res = uniq (sort (filter (\re -> re /= rv) (map (\e -> fromJust $ lookup e g2g) es)))
      in case (lookup rv r) of
        Nothing -> insert rv res r
        Just x -> insert rv (mergeSet res x) r
    cg = foldTrie addGraph Empty g
    ord = fullDfs cg
    in reverse (map (\i -> getIx i ccs) ord)
  result = topSort ccs
  in result
