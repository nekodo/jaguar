import * from '//compiler/prelude.jg'
import * from '//compiler/ast.jg'
import {getUid, setUid, getExports} from '//compiler/pass.jg'

newIdent n = gets >>= \i -> sets (i + 1) >> ret (n ++ '_' ++ intToString i)

rewriteExpr env e = let
  rename n = newIdent n
  renamePat p = case p of
    PConst _ _ -> ret (Pair p empty)
    PVar ann v -> rename v >>= \n -> ret (Pair (PVar ann n) (set v n empty))
    PData ann n ps -> mapM renamePat ps >>= \ps ->
      case (has n env) of
        False -> ret (Pair (PData ann n (map fst ps)) (foldl merge empty (map snd ps)))
        True -> ret (Pair (PData ann (get n env) (map fst ps)) (foldl merge empty (map snd ps)))
  rewritePat p = case p of Pair pat e -> renamePat pat >>= \pe -> case pe of Pair pat penv -> rewriteExpr (merge env penv) e >>= \e -> ret (Pair pat e)
  f e = case e of
    Lam ann p e -> rename p >>= \n -> rewriteExpr (set p n env) e >>= \e -> ret (Right (Lam ann n e))
    Let ann bs e -> rewriteBindings env bs >>= \ebs -> case ebs of
      Pair env bs -> rewriteExpr env e >>= \e -> ret (Right (Let ann bs e))
    Case ann e ps -> rewriteExpr env e >>= \e -> mapM rewritePat ps >>= \ps -> ret (Right (Case ann e ps))
    Var ann v -> case (has v env) of
      True -> ret (Left (Var ann (get v env)))
      False -> ret (Left e)
    New ann tag es -> case (has tag env) of
      True -> ret (Left (New ann (get tag env) es))
      False -> ret (Left e)
    _ -> ret (Left e)
  in breakableDownM f e

rewriteInstance env i = case i of
  (_, Instance ann n t bs) ->
    mapM (\d -> case d of Pair n e -> rewriteExpr env e >>= \e -> ret (Pair n e)) bs >>= \bs -> newIdent '$instance' >>= \insName ->
      ret (insName, Instance ann n t bs)

rewriteBindings env bs = let
  ns = map fst bs
  ns2 = mapM newIdent ns
  in ns2 >>= \ns -> let
    env2 = merge env (toRecord (zip (map fst bs) ns))
    bs2 = mapM (rewriteExpr env2) (map snd bs)
    in bs2 >>= \bs -> ret (Pair env2 (zip ns bs))

renameImport er i = case er of
  Pair env r -> case i of
    ImportOpen _ './builtins.js' _ -> ret (Pair env (push i r))
    ImportOpen ann f ns -> let
      rename er p = case er of
        Pair env r -> case p of
          Pair nf nt -> newIdent nt >>= \n -> ret (Pair (set nt n env) (push (Pair nf n) r))
      in foldM rename (Pair env []) ns >>= \ens -> case ens of
        Pair env ns -> ret (Pair env (push (ImportOpen ann f ns) r))

rewriteModule ms m = case m of
  Module ann fn is ds cs ins bs ->
    foldM renameImport (Pair empty []) is >>= \eis -> case eis of
      Pair env is ->
        rewriteBindings env bs >>= \ebs -> case ebs of
          Pair env bs -> mapM (rewriteInstance env) ins >>= \ins ->
            ret (Module ann fn is ds cs ins bs)

uniquify m = getUid >>= \uid -> getExports >>= \ex -> let
  r = runState uid (rewriteModule ex m)
  in setUid (fst r) >> ret (snd r)
