import * from '//compiler/prelude.jg'

data Ann = Ann Type
emptyAnn = Ann (TUnknown (Ann TBot))

data Expr =
  Var Ann String
  | Const Ann CVal
  | App Ann Expr Expr
  | Lam Ann String Expr
  | Case Ann Expr (Array (Pair Pat Expr))
  | Let Ann (Array (Pair String Expr)) Expr

data CVal =
  CNum Number
  | CStr String

data Pat =
  PVar Ann String
  | PConst Ann CVal
  | PData Ann String (Array Pat)

data Module = Module
  Ann                        // annotations
  (Array Import)             // imports
  (Array Data)               // ADT definitions
  (Array Class)              // type class definitions
  (Array Instance)           // type class instances
  (Array (Pair String Expr)) // value definitions

data Data = Data Ann String (Array String) (Array DataCon)
data DataCon = DataCon Ann String (Array Type)

data Class = Class
  Ann
  String     // name
  String     // type var
  (Array (Pair String Type))

data Instance = Instance
  Ann
  String     // name
  Type       // type for which we're defining the instance
  (Array (Pair String Expr))

data Type =
  TConst Ann String
  | TVar Ann String
  | TApp Ann Type Type
  | TBot
  | TForall Ann String Type
  | TUnknown Ann

data Import =
  ImportClosed Ann String String
  | ImportOpen Ann String (Array String)
  | ImportAll Ann String

dataConName = \dc -> case dc of DataCon _ n ts -> n
dataConNames = \d -> case d of Data _ n ps cs -> map dataConName cs
getExports = \m -> case m of
    Module _ is ds _ _ vs -> concat (concatMap dataConNames ds) (map fst vs)
