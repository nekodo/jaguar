import * from '//compiler/prelude.jg'
import * from '//compiler/parsers.jg'

data LexerState = LexerState
  String // input
  Number // position
  Number // line
  Number // column

data TokenType = WsTok | SymTok | NumTok | StrTok | OpTok | IdTok | ComTok
data Token = Token TokenType String Number Number

mkTok = \t p i -> case i of
  LexerState _1 _2 l c -> apply (\r -> Token t r l c) p i

concatStr = apply (foldl (\cs c -> cs ++ c) '')

runLexer = \p s -> p (LexerState s 0 0 0)

parseChar p s = case s of
  LexerState s i l c -> case (p (getChar i s)) of
    False -> Error 'parser failed'
    True -> case (getChar i s) of
      '\n' -> Success (getChar i s) (LexerState s (i + 1) (l + 1) 0)
      x -> Success (getChar i s) (LexerState s (i + 1) l (c + 1))

anyCharP = parseChar (\c -> True)
charP = \cs -> parseChar (\c -> containsChar c cs)
notCharP = \cs -> parseChar (\c -> not (containsChar c cs))

stringCharP = let 
  newLineP = apply (\c -> '\n') (and (charP '\\') (charP 'n'))
  escapeP = precedes (charP '\\') anyCharP
  notEndOfStringP = notCharP '\''
  in or newLineP (or escapeP notEndOfStringP)
stringP = mkTok StrTok (concatStr (between (charP '\'') (many stringCharP) (charP '\'')))

whitespaceP = mkTok WsTok (concatStr (many1 (charP ' \n')))

intP = concatStr (many1 (charP digits))
numP = mkTok NumTok (apply (\p -> case p of Pair a b -> a ++ b) (and intP (apply (maybe (\n -> '.' ++ n) '') (opt (precedes (charP '.') intP)))))

lineCommentP = mkTok ComTok (concatStr (precedes (and (charP '/') (charP '/')) (many (notCharP '\n'))))

symbolP = mkTok SymTok (charP '()[]{},\\')

identP = mkTok IdTok (apply (\p -> case p of Pair a b -> a ++ b) (and (charP (letters ++ '_')) (concatStr (many (charP (letters ++ digits ++ '_'))))))
opIdentP = mkTok IdTok (concatStr (between (charP '(') (many1 (charP '-+*/=:&|<>^')) (charP ')')))

opP = mkTok OpTok (concatStr (many1 (charP '-+*/=:&|<>^')))

jaguarTokenP = many (or stringP (or whitespaceP (or numP (or lineCommentP (or identP (or opIdentP (or opP symbolP)))))))

tokenize = runLexer jaguarTokenP
